plugins {
    /**
     * resources/main/git.propertiesを生成する
     *
     * @link https://github.com/n0mer/gradle-git-properties
     */
    id "com.gorylenko.gradle-git-properties" version "1.5.2"

    id 'org.hidetake.swagger.generator' version '2.17.0'
    id "com.diffplug.gradle.spotless" version "3.15.0"
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

def appName = "maki_bjs"
def commitHash = ["git",  "rev-parse",  "--short=8", "HEAD"].execute().text.trim()

// applicationのバージョンはx.y.z形式ではなく、git revisionで指定する。
// latest等、期待の実装かどうかは、gitlab上で確認すること。
// ぱっと見が分かりにくいかもだけど、ずーっと1.0.0-SNAPSHOTよりはマシ
version commitHash
group 'jp.co.fujixerox.suiren.traffic'

springBoot {
    // generate build/resources/main/META-INF/build-info.properties
    buildInfo()
}

bootJar {
    // To create a ‘fully executable’ jar
    // https://docs.spring.io/spring-boot/docs/current/reference/html/deployment-install.html
    launchScript()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-redis")
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile "io.springfox:springfox-swagger2:2.9.2"
    compile "io.springfox:springfox-swagger-ui:2.9.2"
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.4'
}

configurations {
    all {
        exclude group: 'org.springframework.security', module: 'spring-security-config'
    }
}

swaggerSources {
    batch {
        inputFile = file('swagger.yaml')
        code {
            language = 'spring'
            configFile = file('swagger.config.json')
            components = ['models', 'apis']
            additionalProperties = ['--type-mappings': 'File=org.springframework.web.multipart.MultipartFile']
        }
    }
}

spotless {
    java {
        googleJavaFormat()
        target '**/*.java'
    }
}

compileJava.dependsOn swaggerSources.batch.code
sourceSets.main.java.srcDir "${swaggerSources.batch.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.batch.code.outputDir}/src/main/resources"

/**
 * jarをS3リポジトリにpublishする設定
 *
 * snapshotの運用ルールが決まっていない(and 恐らく活用されない...)ので、repositories.maven.urlはreleasesのもの固定
 */
publishing {
    def username = System.properties['user.name']
    def releasesRepoUrl = "s3://fx-package/maven/releases"

    publications {
        maven(MavenPublication) {
            artifactId appName
            from components.java

            pom {
                name = appName
                description = "MakiBJS is Batch Job Servicece for Suiren Traffic Man. build by ${username}"
            }

            // 作成されるpomファイルに依存jarを含めないようにする
            // @link https://stackoverflow.com/questions/40268027/how-can-i-exclude-a-dependency-from-a-pom-built-by-the-gradle-maven-publishing-p
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.parent().remove(it)
                }
                pomNode.dependencyManagement.'*'.findAll() {
                    it.parent().remove(it)
                }
            }
        }
    }

    repositories {
        maven {
            url releasesRepoUrl
            credentials(AwsCredentials) {
                accessKey System.getenv('AWS_ACCESS_KEY_ID')
                secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
}
